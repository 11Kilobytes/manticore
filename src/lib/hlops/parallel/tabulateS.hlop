(* tabulateS.hlop
 *
 * COPYRIGHT (c) 2007 The Manticore Project (http://manticore.cs.uchicago.edu)
 * All rights reserved.
 *
 * To build an 'a list out of tabulation of the given (int -> 'a) function.
 *)

#include "types.def"
#include "ropes.def"

define @tabulateS (f     : fun (ml_int / exh -> any), 
                   ml_lo : ml_int, 
                   ml_hi : ml_int / exh : exh) : list =

  let lo : int = #0(ml_lo)
  let hi : int = #0(ml_hi)

  fun build (n : int, acc : list / exh : exh) : list =
    let done : bool = I32Gt (n, hi)
    if done
      then 
        let res : list = @list-rev (acc / exh)
        return (res)
      else 
        let ml_n : ml_int = alloc (n)     
        let x : any = apply f (ml_n / exh)
        let newAcc : list = CONS (x, acc)
        let newN : int = I32Add (n, 1)
        let res : any = apply build (newN, newAcc / exh)
        return (res)

  let tabbed : list = apply build (lo, NIL / exh)
  return (tabbed)
;
