(* touch1.hlop
 *
 * COPYRIGHT (c) 2007 The Manticore Project (http://manticore.cs.uchicago.edu)
 * All rights reserved.
 *
 * To touch a one-toucher future.
 *)

#include "types.def"

define @get-work1 (u : unit / exh : exh) : ();

define @touch1 (fp : future / exh : exh) : any =
  let tmp : any = CAS (&0(fp), EMPTY_F, EVAL_F)
  if Equal (tmp, EMPTY_F)
    then let f : fun (unit / exh -> any) = #1(fp)
         do #1(fp) := (thunk) NIL
         (* may need exn handler for thunk. *)
         (* let exh : exh = ... *)
         let result : any = apply f (UNIT / exh)
         do #0(fp) := result (* one toucher, multiple touches *)
         return (result)
    else if NotEqual (tmp, STOLEN_F)
           then return (tmp)
           else cont k (result: any) = return (result)
	     let tmpX : any = CAS (&0(fp), STOLEN_F, k)
	     if Equal (tmpX, STOLEN_F)
	       then @get-work1 (UNIT / exh)
	       else return (tmpX)
;

(*
  ... original notes from Tues 8/21 ...

  tmp = CAS(&(fp->state), EMPTY_F, EVAL_F);
  if (tmp == EMPTY_F) {
    f = fp->thunk;
    fp->thunk = NIL;
    return f();
  } else if (tmp != STOLEN_F) {
    return tmp;
  } else {
    callcc (fn k =>
      if (BCAS(&(fp->state), STOLEN_F, k)) {
        getWork();
      } else {
        return fp->state;
      })
  }    
*)
