(* list-tab.hlop
 *
 * COPYRIGHT (c) 2007 The Manticore Project (http://manticore.cs.uchicago.edu)
 * All rights reserved.
 *
 * To build an 'a list out of tabulation of the given (int -> 'a) function.
 * A lower limit, an upper limit, and a step size are all given.
 *)

#include "types.def"

define @list-tab (args : [(* f *) fun (ml_int / exh -> any), 
                          (* ml_lo *) ml_int,
                          (* ml_hi *) ml_int,
                          (* ml_step *) ml_int] / exh : exh) : list =

  let f : fun (ml_int / exh -> any) = #0(args)

  let lo : int =
    let t : ml_int = #1(args)
    return (#0(t))

  let hi : int = 
    let t : ml_int = #2(args)
    return (#0(t))

  let step : int =
    let t : ml_int = #3(args)
    return (#0(t))

  fun build (n : int, acc : list / exh : exh) : list =
    let done : bool = I32Gt (n, hi)
    if done
      then 
        let res : list = @list-rev (acc / exh)
        return (res)
      else 
        let ml_n : ml_int = alloc (n)     
        let x : any = apply f (ml_n / exh)
        let newAcc : list = CONS (x, acc)
        let newN : int = I32Add (n, step)
        let res : any = apply build (newN, newAcc / exh)
        return (res)

  let result : list = apply build (lo, NIL / exh)
  return (result)
;
